; The script to gracefully install Darkness under Microsoft Windows.
; 
; This file is part of Darkness project
; Copyright (C) 2008-2009 Darkness team
; 
; This program is free software; you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation; either version 2 of the License, or
; (at your option) any later version.
; 
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
; 
; You should have received a copy of the GNU General Public License
; along with this program; if not, write to the Free Software
; Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA 
;
; $Id$
;
;--------------------------------

SetCompressor /SOLID /FINAL lzma

; The following plugin does not install with NSIS by default.
; Grab it from http://nsis.sourceforge.net/RecFind:_Recursive_FindFirst,_FindNext,_FindClose
!include RecFind.nsh

!include nsDialogs.nsh
!insertmacro NSD_FUNCTION_INIFILE

ShowInstDetails show
ShowUninstDetails show
XPStyle on
AddBrandingImage left 129

Name "Darkness"
Caption "Darkness @DARKNESS_VER_MAJOR@.@DARKNESS_VER_MINOR@.@DARKNESS_VER_PATCH@: @REL_CODE_NAME@"
BrandingText "Darkness Team"
LicenseData @CMAKE_CURRENT_SOURCE_DIR@\copying
LicenseForceSelection checkbox "I accept the terms in the license agreement"

Var Thief1Dir
Var Thief2Dir
Var Shock2Dir

OutFile "darknessSetup.exe"

InstallDir C:\Games\Darkness

InstallDirRegKey HKLM "Software\NSIS_darkness" "Install_Dir"

;Request application privileges for Windows Vista
RequestExecutionLevel admin

LoadLanguageFile "${NSISDIR}\Contrib\Language files\English.nlf"

;--------------------------------

VIProductVersion "@DARKNESS_VER_MAJOR@.@DARKNESS_VER_MINOR@.@DARKNESS_VER_PATCH@.0"
VIAddVersionKey /LANG=${LANG_ENGLISH} "ProductName" "Darkness"
VIAddVersionKey /LANG=${LANG_ENGLISH} "Comments" "Darkness"
VIAddVersionKey /LANG=${LANG_ENGLISH} "CompanyName" "Darkness Team"
VIAddVersionKey /LANG=${LANG_ENGLISH} "LegalTrademarks" "GPL"
VIAddVersionKey /LANG=${LANG_ENGLISH} "LegalCopyright" "© Darkness Team"
VIAddVersionKey /LANG=${LANG_ENGLISH} "FileDescription" "Darkness @DARKNESS_VER_MAJOR@.@DARKNESS_VER_MINOR@.@DARKNESS_VER_PATCH@: @REL_CODE_NAME@"
VIAddVersionKey /LANG=${LANG_ENGLISH} "FileVersion" "@DARKNESS_VER_MAJOR@.@DARKNESS_VER_MINOR@.@DARKNESS_VER_PATCH@"

;--------------------------------

Page Custom ShowWelcome
Page license
Page components
Page directory
Page instfiles
Page Custom SelectGames CreateResourceCfg
PageEx license
   LicenseText "Readme.txt:" "Next >"
   LicenseData @CMAKE_CURRENT_SOURCE_DIR@\installer\Readme.txt
   LicenseForceSelection off
PageExEnd
Page Custom ShowDone

UninstPage uninstConfirm
UninstPage instfiles

;--------------------------------

Function .onInit
	System::Call 'kernel32::CreateMutexA(i 0, i 0, t "myMutex") i .r1 ?e'
		Pop $R0

	StrCmp $R0 0 +3
		MessageBox MB_OK|MB_ICONEXCLAMATION "The Darkness installer is already running."
		Abort
FunctionEnd

Function .onGUIInit
	InitPluginsDir
	File /oname=$PLUGINSDIR\installer.bmp @CMAKE_CURRENT_SOURCE_DIR@\installer\installer.bmp
	SetBrandingImage /RESIZETOFIT $PLUGINSDIR\installer.bmp
FunctionEnd

Function un.onInit
   	System::Call 'kernel32::CreateMutexA(i 0, i 0, t "myMutex") i .r1 ?e'
   		Pop $R0
    
    StrCmp $R0 0 +3
    	MessageBox MB_OK|MB_ICONEXCLAMATION "The Darkness uninstaller is already running."
    	Abort
FunctionEnd

Function un.onGUIInit
	InitPluginsDir
	File /oname=$PLUGINSDIR\installer.bmp @CMAKE_CURRENT_SOURCE_DIR@\installer\installer.bmp
	SetBrandingImage /RESIZETOFIT $PLUGINSDIR\installer.bmp
FunctionEnd

Function ShowWelcome
	nsDialogs::Create /NOUNLOAD 1018
	Pop $0

	${NSD_CreateLabel} 10 10u 100% 80u "Welcome to Darkness @DARKNESS_VER_MAJOR@.@DARKNESS_VER_MINOR@.@DARKNESS_VER_PATCH@ (@REL_CODE_NAME@)."
	Pop $0

	nsDialogs::Show
FunctionEnd

Function ShowDone
	nsDialogs::Create /NOUNLOAD 1018
	Pop $0

	${NSD_CreateLabel} 10 10u 100% 80u "Darkness has been successfully installed on your machine."
	Pop $0

	nsDialogs::Show
FunctionEnd

Function SelectGames	
	File /oname=$PLUGINSDIR\installer.ini "@CMAKE_CURRENT_SOURCE_DIR@\installer\installer.ini"

	StrCpy $0 "$PLUGINSDIR\installer.ini"

	Call CreateDialogFromINI
FunctionEnd

Function CreateResourceCfg
	Call UpdateINIState
	
	ReadINIStr $Thief1Dir "$PLUGINSDIR\installer.ini" "Field 3" "State"
	ReadINIStr $Thief2Dir "$PLUGINSDIR\installer.ini" "Field 5" "State"
	ReadINIStr $Shock2Dir "$PLUGINSDIR\installer.ini" "Field 7" "State"  
	
	StrCmp $Thief1Dir "" 0 DataProvided
	StrCmp $Thief2Dir "" 0 DataProvided
	StrCmp $Shock2Dir "" 0 DataProvided
	MessageBox MB_OK|MB_ICONEXCLAMATION "Please set up at least one directory."
		Abort
	
	DataProvided:
	StrCmp $Thief1Dir "" CheckT2 0	
	FindFirst $2 $1 "$Thief1Dir\Thief.exe"
	StrCmp $1 "" 0 CheckT2
	MessageBox MB_OK|MB_ICONEXCLAMATION "Thief1 path is invalid."
		Abort
	
	CheckT2:
	StrCmp $Thief2Dir "" CheckSS2 0
	FindFirst $2 $1 "$Thief2Dir\Thief2.exe"
	StrCmp $1 "" 0 CheckSS2
	MessageBox MB_OK|MB_ICONEXCLAMATION "Thief2 path is invalid."
		Abort
		
	CheckSS2:
	StrCmp $Shock2Dir "" AllChecked 0
	FindFirst $2 $1 "$Shock2Dir\Shock2.exe"
	StrCmp $1 "" 0 AllChecked
	MessageBox MB_OK|MB_ICONEXCLAMATION "Shock2 path is invalid."
		Abort
	
	AllChecked:
	Call SetupThief1
	Call SetupThief2
	Call SetupShock2	

FunctionEnd

Function SetupThief1
	StrCmp $Thief1Dir "" empty	
	
	FileOpen $8 $INSTDIR\thief1.cfg w			
	FileWrite $8 "[General]$\r$\nFileSystem=.\$\r$\nFileSystem=.\scripts\$\r$\n"	
	FileWrite $8 "FileSystem=.\scripts\Thief1$\r$\nFileSystem=$Thief1Dir$\r$\n"	
	
	${RecFindOpen} "$Thief1Dir" $0 $1
	${RecFindFirst}
	StrCpy $2 $1 4 -4
	StrCmp $2 ".crf" 0 NextFile
	FileWrite $8 "Zip=$Thief1Dir$0\$1$\r$\n"
	NextFile:
	${RecFindNext}
	${RecFindClose}
	SetOutPath $INSTDIR
	CreateShortCut "$SMPROGRAMS\Darkness\Darkness (TDP, TG).lnk" "$INSTDIR\darkness.exe" "T1" "$Thief1Dir\Thief.exe" 0	
	CreateShortCut "$INSTDIR\darkness (Thief 1).lnk" "$INSTDIR\darkness.exe" "T1" "$Thief1Dir\Thief.exe" 0	
	FileClose $8
	empty:	

FunctionEnd

Function SetupThief2
	StrCmp $Thief2Dir "" empty
	
	FileOpen $8 $INSTDIR\thief2.cfg w			
	FileWrite $8 "[General]$\r$\nFileSystem=.\$\r$\nFileSystem=.\scripts\$\r$\n"
	FileWrite $8 "FileSystem=.\scripts\Thief2$\r$\nFileSystem=$Thief2Dir$\r$\n"	
		
	${RecFindOpen} "$Thief2Dir" $0 $1
	${RecFindFirst}
	StrCpy $2 $1 4 -4
	StrCmp $2 ".crf" 0 NextFile
	FileWrite $8 "Zip=$Thief2Dir$0\$1$\r$\n"
	NextFile:
	${RecFindNext}
	${RecFindClose}
	SetOutPath $INSTDIR
	CreateShortCut "$SMPROGRAMS\Darkness\Darkness (TMA).lnk" "$INSTDIR\darkness.exe" "T2" "$Thief2Dir\thief2.exe" 0
	CreateShortCut "$INSTDIR\darkness (Thief 2).lnk" "$INSTDIR\darkness.exe" "T2" "$Thief2Dir\thief2.exe" 0	
	FileClose $8
	empty:	

FunctionEnd

Function SetupShock2
	StrCmp $Shock2Dir "" empty
	
	FileOpen $8 $INSTDIR\shock2.cfg w			
	FileWrite $8 "[General]$\r$\nFileSystem=.\$\r$\nFileSystem=.\scripts\$\r$\n"
	FileWrite $8 "FileSystem=.\scripts\Shock2$\r$\nFileSystem=$Shock2Dir$\r$\n"	
		
	${RecFindOpen} "$Shock2Dir" $0 $1
	${RecFindFirst}
	StrCpy $2 $1 4 -4
	StrCmp $2 ".crf" 0 NextFile
	FileWrite $8 "Zip=$Shock2Dir$0\$1$\r$\n"
	NextFile:
	${RecFindNext}
	${RecFindClose}
	SetOutPath $INSTDIR
	CreateShortCut "$SMPROGRAMS\Darkness\Darkness (SS2).lnk" "$INSTDIR\darkness.exe" "SS2" "$Shock2Dir\Shock2.exe" 0
	CreateShortCut "$INSTDIR\darkness (Shock 2).lnk" "$INSTDIR\darkness.exe" "SS2" "$Shock2Dir\Shock2.exe" 0
	FileClose $8
	empty:	

FunctionEnd

;--------------------------------

Section "Darkness (required)"	

	SectionIn RO

	SetOutPath $INSTDIR
	File @PROJECT_BINARY_DIR@\src\main\release\darkness.exe
	File @PROJECT_BINARY_DIR@\src\main\release\darknessScript.exe
	File @PROJECT_BINARY_DIR@\src\main\release\OpenDark.dll
	File /oname=darkness.cfg @CMAKE_CURRENT_SOURCE_DIR@\installer\darkness-sample.cfg
	File @CMAKE_CURRENT_SOURCE_DIR@\installer\readme.txt
	File "$%OGRE_HOME%\bin\release\*.dll"	
	File "$%OGRE_HOME%\bin\release\Plugins.cfg"
	File "@FREEIMAGE_PATH@\Dist\FreeImage.dll"
	FILE /x py*_d.dll "$%WinDir%\System32\Py*.dll"
	
	SetOutPath $INSTDIR\Scripts
	File /r /x .svn "@CMAKE_CURRENT_SOURCE_DIR@\Scripts\*.*"
	

	WriteRegStr HKLM SOFTWARE\NSIS_darkness "Install_Dir" "$INSTDIR"

	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\darkness" "DisplayName" "NSIS darkness"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\darkness" "UninstallString" '"$INSTDIR\uninstall.exe"'
	WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\darkness" "NoModify" 1
	WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\darkness" "NoRepair" 1
	WriteUninstaller "uninstall.exe"
  
SectionEnd

Section "Start Menu Shortcuts"

	SetOutPath $INSTDIR
	CreateDirectory "$SMPROGRAMS\Darkness"			
	CreateShortCut "$SMPROGRAMS\Darkness\Uninstall.lnk" "$INSTDIR\uninstall.exe" "" "$INSTDIR\uninstall.exe" 0
  
SectionEnd

;--------------------------------

; Uninstaller

Section "Uninstall"
  
	; Remove registry keys
	DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\darkness"
	DeleteRegKey HKLM SOFTWARE\NSIS_darkness

	; Remove files and uninstaller
	Delete $INSTDIR\darkness.exe
	Delete $INSTDIR\darknessScript.exe
	Delete $INSTDIR\*.cfg
	Delete $INSTDIR\*.log
	Delete $INSTDIR\*.dll
	Delete $INSTDIR\*.bmp
	Delete $INSTDIR\*.lnk
	Delete $INSTDIR\*.txt
	Delete $INSTDIR\uninstall.exe

	; Remove shortcuts, if any
	Delete "$SMPROGRAMS\Darkness\*.*"

	; Remove directories used
	RMDir "$SMPROGRAMS\Darkness"
	RMDir /r "$INSTDIR\Scripts\"
	RMDir "$INSTDIR"


SectionEnd